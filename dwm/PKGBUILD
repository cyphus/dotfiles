# $Id: PKGBUILD 60970 2011-12-19 21:33:58Z spupykin $
# Original Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Mutilator: Terrance Kennedy <cyphus _at_ cyph _dot_ us>

pkgname=cyphus-dwm-git
_gitname=cyphus-dwm
pkgver=1621.6b3c5ee
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
conflicts=('dwm')
depends=('libx11' 'libxinerama')
makedepends=('git')
source=( 'git+https://github.com/tkennedy/cyphus-dwm.git' )

pkgver() {
  cd $_gitname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $_gitname
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $_gitname
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D README $pkgdir/usr/share/doc/$pkgname/README
}
